apply plugin: 'maven-publish'

group = 'io.github.yavski'
version = android.defaultConfig.versionName

publishing {
    publications {
        maven(MavenPublication){
            groupId project.group
            artifactId rootProject.name
            version project.version
            artifact("${buildDir}/outputs/aar/${project.name}-release.aar")

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.compile.allDependencies.each {
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}